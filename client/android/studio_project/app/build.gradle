apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.14'
    }
}

android {
    signingConfigs {
        debug {
            keyAlias 'turtle nest reporter'
            keyPassword 'amitwsm'
            // Download here: https://drive.google.com/file/d/0B4ZK1C9FL9MBTWlmZkxKNUZ6NE0/view
            // DO NOT checkin.
            storeFile file('./app.keystore')
            storePassword 'amitwsm'
        }
    }
    lintOptions {
        abortOnError true
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.islandturtlewatch.nest.reporter"
        minSdkVersion 16
        targetSdkVersion 29
        multiDexEnabled true
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    configurations {
        all {
            exclude module: 'commons-logging'
            exclude module: 'httpclient'
            /*
            exclude module: 'protobuf-lite'
            exclude module: 'protobuf-javalite'
            exclude module: 'protolite-well-known-types'
             */
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        //main.java.srcDirs = ['../../src/src/', '../../../../proto/generated/']
        main.java.srcDirs = ['../../src/src/']
        main.proto.srcDirs = ['../../../../proto']
    }

    // We use this to upload images.
    useLibrary  'org.apache.http.legacy'
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // androidTestCompile compiles instrumentation tests written using Espresso
    // used by Firebase Test Lab
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'com.google.api-client:google-api-client:1.25.0'
    implementation 'com.google.api-client:google-api-client-android:1.25.0'
    implementation 'com.google.http-client:google-http-client-android:1.25.0'
    implementation 'com.google.http-client:google-http-client-gson:1.25.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    annotationProcessor 'org.projectlombok:lombok:1.16.2'
    compileOnly 'org.projectlombok:lombok:1.16.2'
    implementation 'joda-time:joda-time:2.3'
    implementation("com.google.protobuf:protobuf-javalite") {
        version {
            // The version packaged with firebase has broken serialization :( 
            strictly '3.11.0'
        }
    }
    //implementation 'com.google.protobuf:protobuf-java:3.14.0'
    implementation 'org.apache.httpcomponents:httpcore:4.4.10'
    implementation 'org.apache.httpcomponents:httpmime:4.5.6'
    implementation 'com.google.android.gms:play-services-identity:17.0.0'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-base:17.5.0'

    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    implementation 'com.google.firebase:firebase-firestore:22.0.1'

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}
